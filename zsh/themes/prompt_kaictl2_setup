+vi-git-status () {
  # Untracked files.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[misc]='%F{r}?%f'
  fi
}

prompt_kaictl2_precmd(){
  vcs_info
}

prompt_kaictl2_help(){
  cat <<EOH
kaictl's new prompt
EOH
}

prompt_kaictl2_setup() {
  setopt prompt_subst
  autoload -U colors && colors
  autoload -Uz add-zsh-hook vcs_info

  prompt_opts=(cr percent subst)
  add-zsh-hook precmd prompt_kaictl2_precmd

  zstyle ':vcs_info:*' enable git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%B%F{g}@%f%b'
  zstyle ':vcs_info:*' unstagedstr '%F{y}!%f'
  zstyle ':vcs_info:*' formats '%F{m}%b%c%u%m%F{n}'
  zstyle ':vcs_info:*' actionformats "%b%c%u|%F{c}%a%f"
  zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{c}%r%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git-status

  if (( ! UID )); then
    __name='%B%F{red}%n%f%b'
  else
    __name='%B%F{blue}%n%f%b'
  fi

  if [[ -n $SSH_CONNECTION ]]; then
    __name='%B%F{yellow}%n%f%b'
  fi

  PROMPT="%(?,,%F{red}%?%f|)$__name@%F{green}%m%f:%F{yellow}%~%f %# "
  RPROMPT='${vcs_info_msg_0_}'

  #RPROMPT="%(?.:D.:()"
  PS4='+%N:%i:%x:%I>'
}

prompt_kaictl2_preview(){
  prompt_preview_theme kaictl2 "$@"
}
prompt_kaictl2_setup "$@"

# vim: set ft=zsh:
